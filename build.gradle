group = 'org.sagebionetworks'
ext.name = 'dashboard'
version = '0.7.1-' + (new Date()).format(
        'yyyyMMddHHmm', TimeZone.getTimeZone("UTC"))

/*** Java ***/

apply plugin: 'java'

jar {
    group = project.group
    baseName = project.ext.name
    version = project.version
}

repositories {
    mavenCentral()
    maven {
        url "http://sagebionetworks.artifactoryonline.com/sagebionetworks/libs-releases-local"
    }
}

dependencies {
    compile 'org.sagebionetworks:dashboard-core:0.1.5+'
    compile 'org.springframework:spring-context:' + springFrameworkVersion
    compile 'org.springframework:spring-jdbc:' + springFrameworkVersion
    compile 'org.apache.commons:commons-dbcp2:2.1'
    compile 'org.springframework.data:spring-data-redis:' + springDataRedisVersion
    compile 'redis.clients:jedis:' + jedisVersion
    compile 'joda-time:joda-time:2.7'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.amazonaws:aws-java-sdk-s3:' + awsJavaSdkVersion
    compile 'org.apache.httpcomponents:httpclient:4.4.1'
    compile 'org.slf4j:slf4j-api:' + slf4jVersion
    runtime 'ch.qos.logback:logback-classic:' + logbackVersion
    runtime 'ch.qos.logback:logback-core:' + logbackVersion
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.springframework:spring-test:' + springFrameworkVersion
}

/*** Eclipse ***/

apply plugin: 'eclipse'

/*** Command-line Application ***/

apply plugin: 'application'
mainClassName = "org.sagebionetworks.dashboard.App"
run {
    args = [filePath]
}

/*** Code Quality Tools ***/

apply plugin: 'findbugs'
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jdepend'
tasks.withType(JDepend) {
    reports {
        xml.enabled = false
        text.enabled = true
    }
}

apply plugin: 'pmd'
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

/*** Artifactory ***/

buildscript {
    repositories {
         jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
    }
}

apply plugin: 'com.jfrog.artifactory'
artifactory {
    contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
    publish {
        contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
        repository {
            repoKey = 'libs-releases-local'
            username = "${System.env.ARTI_USR}"
            password = "${System.env.ARTI_PWD}"
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

/*** Maven Publishing ***/

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

/*** Wrapper ***/

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
