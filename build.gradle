group = 'org.sagebionetworks'
project.ext.baseName = 'dashboard'
version = '0.5.25-' + (new Date()).format(
        'yyyyMMddHHmmss', TimeZone.getTimeZone("UTC"))

/*** Java ***/

apply plugin: 'java'

jar {
    group = group
    baseName = baseName
    version = version
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework:spring-context:' + springFrameworkVersion
    compile 'org.springframework:spring-jdbc:' + springFrameworkVersion
    compile 'org.apache.commons:commons-dbcp2:2.0.1'
    compile 'org.springframework.data:spring-data-redis:' + springDataRedisVersion
    compile 'redis.clients:jedis:' + jedisVersion
    compile 'joda-time:joda-time:2.3'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.amazonaws:aws-java-sdk:1.8.3'
    compile 'org.apache.httpcomponents:httpclient:4.3.4'
    compile 'org.postgresql:postgresql:9.3-1101-jdbc41'
    compile 'org.jasypt:jasypt:1.9.2'
    compile 'org.slf4j:slf4j-api:' + slf4jVersion
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    runtime 'ch.qos.logback:logback-core:1.1.2'
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.springframework:spring-test:' + springFrameworkVersion
    testRuntime 'org.slf4j:slf4j-log4j12:' + slf4jVersion
    testRuntime 'log4j:log4j:1.2.17'
}

/*** Eclipse ***/

apply plugin: 'eclipse'

/*** Command-line Application ***/

apply plugin: 'application'
mainClassName = "org.sagebionetworks.dashboard.App"
run {
    args = [filePath]
}

/*** Code Quality Tools ***/

apply plugin: 'findbugs'
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jdepend'
tasks.withType(JDepend) {
    reports {
        xml.enabled = false
        text.enabled = true
    }
}

apply plugin: 'pmd'
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

/*** Artifactory ***/

buildscript {
    repositories {
         jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.4'
    }
}

apply plugin: 'artifactory-publish'

artifactory {
    contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
    publish {
        contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
        repository {
            repoKey = 'libs-releases-local'
            username = "${System.env.ARTI_USR}"
            password = "${System.env.ARTI_PWD}"
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

/*** Maven Publishing ***/

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

/*** Wrapper ***/

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
